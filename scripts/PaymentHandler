#!/usr/bin/env perl
use strict;
use warnings;
use Mojolicious::Lite -signatures;
use JSON;
use Scalar::Util qw(looks_like_number);
use Mojo::Redis;
use Mojo::Pg;
use Data::Dumper;
use Mojo::File qw(curfile);
use lib curfile->dirname->sibling('lib')->to_string;

my $redis_url = defined($ENV{"APP_ENV"}) && $ENV{"APP_ENV"} eq 'docker' ? 'redis://redis:6379' : 'redis://localhost:6379';
my $redis = Mojo::Redis->new($redis_url);
my $postgres = Mojo::Pg->new($ENV{"POSTGRES_DSN"});

post '/payments' => sub ($c) {
  my $json_params = $c->req->json;
  my $correlationId = $json_params->{'correlationId'};
  my $amount = $json_params->{'amount'};
  return $c->render(json => { data => 'Missing correlationId' }, status => 400) unless $correlationId;
  return $c->render(json => { data => 'Missing correct amount' }, status => 400) if !looks_like_number($amount);
  my $pubsub = $redis->pubsub;
  my $db = $pubsub->db;
  $db->lpush('payment_process_queue', JSON::encode_json {correlationId => $correlationId, amount => $amount}, sub {
    my ($db, $err, $res) = @_;
    return $c->render(json => { data => 'queued' }, status => 500) if $err;
    $c->render(json => { data => 'queued' }, status => 200);
  });
};

get '/payments-summary' => sub ($c) {
  my $from = defined $c->param('from') ? $c->param('from') : '';
  my $to = defined $c->param('to') ?  $c->param('to') : '';
  my $database = $postgres->db;
  my @binds;
  my $query = "
    SELECT json_object_agg(processor, summary) AS result
    FROM (
      SELECT 
        p.processor,
        json_build_object(
          'totalRequests', COUNT(payments.processor)::INT,
          'totalAmount', COALESCE(ROUND(SUM(payments.amount)::numeric, 2), 0)
        ) AS summary
      FROM (VALUES ('default'), ('fallback')) AS p(processor)
      LEFT JOIN payments ON payments.processor = p.processor
  ";
  if (defined $from && $from ne '') {
    push @binds, $from;
    $query .= " AND payments.requested_at >= ? ";
  }
  if (defined $to && $to ne '') {
    push @binds, $to;
    $query .= " AND payments.requested_at <= ? ";
  }
  $query .= "
      GROUP BY p.processor
    ) AS final_summary
  ";
  my $result = $database->query($query, @binds)->hash;
  my $parsed = decode_json $result->{result} || {};
  $c->render(json => $parsed, status => 200);
};


app->start;
